apply plugin: 'scala'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply from: 'ide-support.gradle'
apply from: "dependencies.gradle"

ext.version = "0.29.0"

dependencies {
    compile scalaLibrary
    compile slf4jApi, servletApi, jodaTime, jodaConvert

    testCompile jettyEmbedded, mockito, junit
    testCompile logback, jsontools, javastubserver

    testCompile(scalatest) {
        exclude group: 'org.scala-lang'
    }
}

subprojects {
    group = 'uk.co.bigbeeconsultants'
    sourceCompatibility = "1.7"
    targetCompatibility = "1.7"
}

task ci(type: GradleBuild) {
    description = 'Everything you need to run for continuous integration'
    group = "CI"
    tasks = ['clean', 'build', 'uploadArchives', 'jacocoTestReport']
}

archivesBaseName = 'bee-client_' + scalaVersion
jar.enabled = true

clean.delete = ["build", "logs", "out", "reports"]

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false // Zinc incremental compiler
}

ext.uploadPath = "file:local/repo"

uploadArchives {
    repositories {
        mavenDeployer {
            repositories {
                repository(url: rootProject.ext.uploadPath)
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}
